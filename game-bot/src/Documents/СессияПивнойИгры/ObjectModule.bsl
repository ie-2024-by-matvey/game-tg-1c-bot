
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура РассчитатьРаунд(НомерРаунда) Экспорт
	
	Если НомерРаунда > ХодИгры.Количество() Тогда
		ВызватьИсключение НСтр("ru = 'Раунд еще не начат'");
	КонецЕсли;
	
	ДанныеРаунда = ХодИгры[НомерРаунда - 1];
	
	Если НомерРаунда - СрокДоставки < 1 Тогда
		ПоступившиеОтправки = Неопределено;
	Иначе
		ПоступившиеОтправки = ХодИгры[НомерРаунда - СрокДоставки];
	КонецЕсли;
	
	Если НомерРаунда = 1 Тогда
		ПредыдущийРаунд = Неопределено;
	Иначе
		ПредыдущийРаунд = ХодИгры[НомерРаунда - 2];
	КонецЕсли;
	
	Если НомерРаунда = 1 Тогда
		ДанныеРаунда.Спрос = НачальныйСпрос;
	Иначе
		ДанныеРаунда.Спрос = ПредыдущийРаунд.Спрос;
		Фильтр = Новый Структура;
		Фильтр.Вставить("НомерРаунда", НомерРаунда);
		НайденныеСтроки = ИзмененияСпроса.НайтиСтроки(Фильтр);
		Для Каждого Строка Из НайденныеСтроки Цикл
			ДанныеРаунда.Спрос = ДанныеРаунда.Спрос + Строка.Изменение;
		КонецЦикла;
	КонецЕсли;
	
	Для Сч = 1 По 4 Цикл
		
		// Рассчитываем поступления
		Если ПоступившиеОтправки = Неопределено Тогда
			Поступление = СтартовыеЗаказы;
		Иначе
			Если Сч = 1 Тогда
				Поступление = ПоступившиеОтправки.Заказ1;
			Иначе
				Поступление = ПоступившиеОтправки["Отправка" + (Сч - 1)];
			КонецЕсли;
		КонецЕсли;
		
		Если ПредыдущийРаунд = Неопределено Тогда
			ЗапасНаНачало = СтартовыеЗапасы;
		Иначе
			ЗапасНаНачало = ПредыдущийРаунд["Запас" + Сч];
		КонецЕсли;
		
		Запас = ЗапасНаНачало + Поступление;
		
		// Рассчитываем отправку
		Если Сч = 4 Тогда
			НеобходимоОтправить = ДанныеРаунда.Спрос;
		Иначе
			НеобходимоОтправить = ДанныеРаунда["Заказ" + (Сч + 1)];
		КонецЕсли;
		
		Если ПредыдущийРаунд <> Неопределено Тогда
			НеобходимоОтправить = НеобходимоОтправить + ПредыдущийРаунд["Долг" + Сч];
		КонецЕсли;
		
		Отправлено = Мин(НеобходимоОтправить, Запас);
		
		Запас = Запас - Отправлено;
		
		Долг = НеобходимоОтправить - Отправлено;	
		
		ДанныеРаунда["Штраф" + Сч] = Запас * ШтрафЗаИзбыток + Долг * ШтрафЗаНарушениеЗаказа;
			
		ДанныеРаунда["Отправка" + Сч] = Отправлено;
		ДанныеРаунда["Запас" + Сч] = Запас;
		ДанныеРаунда["Долг" + Сч] = Долг;
		
		
	КонецЦикла;			
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий
//Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
//Код процедур и функций
#КонецОбласти

#КонецЕсли