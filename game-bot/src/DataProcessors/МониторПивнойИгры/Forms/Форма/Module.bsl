
&НаСервере
Процедура ОбновитьНаСервере()
	
	ДиаграммаОстатков.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	СессияПивнойИгрыХодИгры.Запас1 КАК Запас1,
	               |	СессияПивнойИгрыХодИгры.Долг1 КАК Долг1,
	               |	СессияПивнойИгрыХодИгры.Запас2 КАК Запас2,
	               |	СессияПивнойИгрыХодИгры.Долг2 КАК Долг2,
	               |	СессияПивнойИгрыХодИгры.Запас3 КАК Запас3,
	               |	СессияПивнойИгрыХодИгры.Долг3 КАК Долг3,
	               |	СессияПивнойИгрыХодИгры.Запас4 КАК Запас4,
	               |	СессияПивнойИгрыХодИгры.Долг4 КАК Долг4,
	               |	СессияПивнойИгрыХодИгры.Ссылка.Номер КАК Номер,
	               |	СессияПивнойИгрыХодИгры.НомерСтроки КАК НомерСтроки
	               |ИЗ
	               |	Документ.СессияПивнойИгры.ХодИгры КАК СессияПивнойИгрыХодИгры
	               |ГДЕ
	               |	(СессияПивнойИгрыХодИгры.НомерСтроки < СессияПивнойИгрыХодИгры.Ссылка.ТекущийРаунд
	               |			ИЛИ СессияПивнойИгрыХодИгры.Ссылка.Статус = &Статус)
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	НомерСтроки,
	               |	Номер";
	
	Запрос.УстановитьПараметр("Статус", Перечисления.СтатусИгровойСессии.Завершена);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Точка = ДиаграммаОстатков.УстановитьТочку(Выборка.НомерСтроки);		
		
		Для Сч = 1 По 4 Цикл
			Серия = ДиаграммаОстатков.УстановитьСерию(УдалитьЛидирующиеНули(Выборка.Номер) + "-" + Сч);
			ДиаграммаОстатков.УстановитьЗначение(Точка, Серия, Выборка["Запас" + Сч] - Выборка["Долг" + Сч]);
		КонецЦикла;
		
	КонецЦикла;
	
	ДиаграммаШтрафов.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТекущиеЗначения.Ссылка.Номер КАК Номер,
	               |	ТекущиеЗначения.НомерСтроки КАК НомерСтроки,
	               |	СУММА(ПредыдущиеЗначения.Штраф1) КАК Штраф1,
	               |	СУММА(ПредыдущиеЗначения.Штраф2) КАК Штраф2,
	               |	СУММА(ПредыдущиеЗначения.Штраф3) КАК Штраф3,
	               |	СУММА(ПредыдущиеЗначения.Штраф4) КАК Штраф4
	               |ИЗ
	               |	Документ.СессияПивнойИгры.ХодИгры КАК ТекущиеЗначения
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СессияПивнойИгры.ХодИгры КАК ПредыдущиеЗначения
	               |		ПО ТекущиеЗначения.Ссылка = ПредыдущиеЗначения.Ссылка
	               |			И ТекущиеЗначения.НомерСтроки >= ПредыдущиеЗначения.НомерСтроки
	               |ГДЕ
	               |	(ТекущиеЗначения.НомерСтроки < ТекущиеЗначения.Ссылка.ТекущийРаунд
	               |			ИЛИ ТекущиеЗначения.Ссылка.Статус = &Статус)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ТекущиеЗначения.Ссылка.Номер,
	               |	ТекущиеЗначения.НомерСтроки
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	НомерСтроки,
	               |	Номер";
	
	Запрос.УстановитьПараметр("Статус", Перечисления.СтатусИгровойСессии.Завершена);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Точка = ДиаграммаШтрафов.УстановитьТочку(Выборка.НомерСтроки);		
		
		Для Сч = 1 По 4 Цикл
			Серия = ДиаграммаШтрафов.УстановитьСерию(УдалитьЛидирующиеНули(Выборка.Номер) + "-" + Сч);
			ДиаграммаШтрафов.УстановитьЗначение(Точка, Серия, Выборка["Штраф" + Сч]);
		КонецЦикла;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 10
	               |	Т.Игрок КАК Игрок,
	               |	СУММА(Т.Штраф) КАК Штраф,
	               |	МАКСИМУМ(Т.ТекущийРаунд) КАК КоличествоРаундов
	               |ИЗ
	               |	(ВЫБРАТЬ
	               |		СессияПивнойИгрыХодИгры.Ссылка.Производитель КАК Игрок,
	               |		СессияПивнойИгрыХодИгры.Штраф1 КАК Штраф,
	               |		СессияПивнойИгрыХодИгры.Ссылка.ТекущийРаунд КАК ТекущийРаунд
	               |	ИЗ
	               |		Документ.СессияПивнойИгры.ХодИгры КАК СессияПивнойИгрыХодИгры
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		СессияПивнойИгрыХодИгры.Ссылка.Дистрибьютор,
	               |		СессияПивнойИгрыХодИгры.Штраф2,
	               |		СессияПивнойИгрыХодИгры.Ссылка.ТекущийРаунд
	               |	ИЗ
	               |		Документ.СессияПивнойИгры.ХодИгры КАК СессияПивнойИгрыХодИгры
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		СессияПивнойИгрыХодИгры.Ссылка.ОптовыйПродавец,
	               |		СессияПивнойИгрыХодИгры.Штраф3,
	               |		СессияПивнойИгрыХодИгры.Ссылка.ТекущийРаунд
	               |	ИЗ
	               |		Документ.СессияПивнойИгры.ХодИгры КАК СессияПивнойИгрыХодИгры
	               |	
	               |	ОБЪЕДИНИТЬ ВСЕ
	               |	
	               |	ВЫБРАТЬ
	               |		СессияПивнойИгрыХодИгры.Ссылка.Ритейлер,
	               |		СессияПивнойИгрыХодИгры.Штраф4,
	               |		СессияПивнойИгрыХодИгры.Ссылка.ТекущийРаунд
	               |	ИЗ
	               |		Документ.СессияПивнойИгры.ХодИгры КАК СессияПивнойИгрыХодИгры) КАК Т
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Т.Игрок
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Штраф";
	
	Лидеры.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Функция УдалитьЛидирующиеНули(Строка)
	
	Результат = Строка;
	
	Пока СтрНачинаетсяС(Результат, "0") Цикл
		Результат = Прав(Результат, СтрДлина(Результат - 1));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПодключитьОбработчикОжидания("ОбновитьПоТаймеру", 5);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПоТаймеру()
	
	ОбновитьНаСервере();
	
КонецПроцедуры
